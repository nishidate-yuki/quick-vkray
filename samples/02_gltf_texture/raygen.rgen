#version 460
#extension GL_EXT_ray_tracing : enable

layout(binding = 0, set = 0) uniform accelerationStructureEXT topLevelAS;
layout(binding = 1, set = 0, rgba8) uniform image2D image;

layout(location = 0) rayPayloadEXT vec3 payLoad;

void main()
{
    float theta = radians(-30);
    mat4 rotX = mat4(
        1, 0,          0,           0,
        0, cos(theta), -sin(theta), 0,
        0, sin(theta), cos(theta),  0,
        0, 0,          0,           1
    );

    float phi = radians(-30);
    mat4 rotY = mat4(
        cos(phi), 0, sin(phi), 0,
        0,        1, 0,        0,
        -sin(phi), 0, cos(phi), 0,
        0, 0, 0, 1
    );

	const vec2 pixelCenter = vec2(gl_LaunchIDEXT.xy) + vec2(0.5);
	const vec2 inUV = pixelCenter/vec2(gl_LaunchSizeEXT.xy);
	vec2 d = inUV * 2.0 - 1.0;

	vec4 origin = rotY * rotX * vec4(0, 0, 5, 1);
	vec4 target = rotY * rotX * vec4(d.x, d.y, 3, 1) ;
	vec4 direction = vec4(normalize(target.xyz - origin.xyz), 0) ;

	float tmin = 0.001;
	float tmax = 10000.0;

    payLoad = vec3(0.0);

    traceRayEXT(
        topLevelAS,
        gl_RayFlagsOpaqueEXT,
        0xff, // cullMask
        0,    // sbtRecordOffset
        0,    // sbtRecordStride
        0,    // missIndex
        origin.xyz,
        tmin,
        direction.xyz,
        tmax,
        0     // payloadLocation
    );

	imageStore(image, ivec2(gl_LaunchIDEXT.xy), vec4(payLoad, 0.0));
}
